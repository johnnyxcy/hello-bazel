load("@aspect_rules_js//js:defs.bzl", "js_run_binary")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//projects/js/hello:vite/package_json.bzl", vite = "bin")
load("@npm//projects/js/hello:vitest/package_json.bzl", vitest = "bin")

npm_link_all_packages(name = "node_modules")

filegroup(
    name = "src",
    srcs = glob(
        include = ["src/**"],
        exclude = ["**/*.test.ts"],
    ),
)

filegroup(
    name = "src_test",
    srcs = glob(
        include = ["src/**/*.test.ts"],
    ),
)

runtime_deps = [
    "package.json",
    "tsconfig.json",
    "tsconfig.node.json",
]

node_modules_deps = [
    ":node_modules/vite",
    ":node_modules/rollup-plugin-visualizer",
    ":node_modules/vite-plugin-dts",
]

node_modules_deps_test = [
    ":node_modules/vitest",
    ":node_modules/@vitest/coverage-istanbul",
]

vite.vite_binary(
    name = "vite",
    chdir = package_name(),
    data = ["vite.config.mts"],
)

js_run_binary(
    name = "hello",
    srcs = [":src"] + runtime_deps + node_modules_deps,
    args = ["build"],
    mnemonic = "ViteBuild",
    out_dirs = [
        ".dist",
        ".build",
    ],
    tool = ":vite",
)

vitest.vitest_test(
    name = "test",
    args = [
        "run",
        "--coverage",
    ],
    chdir = package_name(),
    data = [
        "vite.config.mts",
        ":src",
        ":src_test",
    ] + runtime_deps + node_modules_deps + node_modules_deps_test,
)

vitest.vitest_binary(
    name = "test.ui",
    args = [
        "--ui",
        "--coverage",
    ],
    chdir = package_name(),
    data = [
        "vite.config.mts",
        ":hello",
        ":node_modules/@vitest/ui",
        ":src",
        ":src_test",
    ] + runtime_deps + node_modules_deps + node_modules_deps_test,
)
