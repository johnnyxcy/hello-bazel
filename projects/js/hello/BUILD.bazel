load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//projects/js/hello:vite/package_json.bzl", vite = "bin")
load("@npm//projects/js/hello:vitest/package_json.bzl", vitest = "bin")

npm_link_all_packages(name = "node_modules")

filegroup(
    name = "src",
    srcs = glob(
        include = ["src/**"],
        exclude = ["**/*.test.ts"],
    ),
)

filegroup(
    name = "src_test",
    srcs = glob(
        include = ["src/**/*.test.ts"],
    ),
)

runtime_deps = [
    "vite.config.mts",
    "package.json",
    "tsconfig.json",
    "tsconfig.node.json",
]

node_modules_deps = [
    ":node_modules/vite",
    ":node_modules/chalk",
    ":node_modules/rollup-plugin-visualizer",
    ":node_modules/vite-plugin-dts",
]

node_modules_deps_test = [
    ":node_modules/vitest",
    ":node_modules/@vitest/coverage-istanbul",
]

vite.vite(
    name = "build",
    srcs = [":src"] + runtime_deps + node_modules_deps,
    args = ["build"],
    chdir = package_name(),
    out_dirs = [
        ".dist",
        ".build",
    ],
)

npm_package(
    name = "hello",
    srcs = [
        "package.json",
        ":build",
    ],
    visibility = ["//visibility:public"],
)

vitest.vitest_test(
    name = "test",
    args = [
        "run",
        "--coverage",
    ],
    chdir = package_name(),
    data = [
        ":src",
        ":src_test",
    ] + runtime_deps + node_modules_deps + node_modules_deps_test,
)

vitest.vitest_binary(
    name = "test.ui",
    args = [
        "--ui",
        "--coverage",
    ],
    chdir = package_name(),
    data = [
        ":src",
        ":src_test",
    ] + runtime_deps + node_modules_deps + node_modules_deps_test,
)
